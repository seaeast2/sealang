Sea lang specification.


==TOKEN================================================================ 
<IMPORT> : import 
<VOID> : void 
<UNSIGNED> : unsigned 
<BOOL> : bool
<CHAR> : char 
<SHORT> : short 
<INT> : int 
<LONG> : long 
<CLASS> : class 
<FLOAT> : float 
<DOUBLE> : double
<STATIC> : static 
<IDENTIFIER> : [a-zA-Z][a-zA-Z0-9]* 
<INTEGER> : [0-9]* 
<CHARACTER> : [a-zA-Z0-9]  \\ 'A'
<STRING> : "\""[a-zA-Z0-9]*"\""  \\ "test"
<EOF> : End of file 
<TYPEDEF> : typedef 
<IF> : if 
<WHILE> : while 
<FOR> : for 
<BREAK> : break 
<RETURN> : return 
<SIZEOF> : sizeof 

==DECLARATION========================================================= 
compilation_unit 
  : import_stmts top_defs <EOF> 

typeref_base  
  : <VOID> 
    | <CHAR> 
    | <SHORT> 
    | <INT> 
    | <LONG> 
    | <UNSIGNED> <CHAR> 
    | <UNSIGNED> <SHORT> 
    | <UNSIGNED> <INT> 
    | <UNSIGNED> <LONG> 
    | <FLOAT> 
    | <DOUBLE>
    | <CLASS> <IDENTIFIER> 

import_stmts  
  : (import_stmt)* 

import_stmt 
  : <IMPORT> name ("." name)* ";" 

name 
  : <IDENTIFIER> 

top_defs // top definitions 
  : ( deffunc 
    | defvars 
    | defconst 
    | defclass 
    | typedef )* 

defvars // variable definition. ex) int a = 0, b=19; 
  : storage type name ["=" expr] [("," name ["=" expr])*] ";" 

defconst
  : <CONST> type name "=" expr ";"

storage 
  : [<STATIC>] 

deffunc // function definition 
  : storage typeref name "(" params ")" block 

params // parameter definition 
  : <VOID> 
  | fixedparams ["," "..."] 

fixedparams // fixed parameter definition 
  : param ("," param)* 

param 
  : type name 

defvar_list 
  : (defvars)*

block 
  : "{" defvar_list stmts "}" 

defclass // class definition 
  : <CLASS> name member_list ";" 

member_list //  
  : "{" (class_member ";")* "}" 

class_member // class member definition 
  : type name 
  | deffunc

typedef // ex) typedef int i32; 
  : <TYPEDEF> typeref <IDENTIFIER> ";" 

type 
  : typeref 

// TODO : need to merge type and typeref. typeref is not needed.
typeref 
  : typeref_base  ( "[""]"                    // unassigned array
                  | "["<INTEGER>"]"           // assigned array
                  | "*"                       // pointer 
                  | "(" param_typerefs ")")*  // function pointer // TODO : need to seperate function type

param_typerefs // function pointer param type definition 
  : <VOID> 
  | type ("," type)* ["," "..."] 
 

stmts 
  : (stmt)* 

stmt 
  :";" 
  | labeled_stmt 
  | expr ";" 
  | block 
  | if_stmt 
  | while_stmt 
  | dowhile_stmt 
  | for_stmt 
  | switch_stmt 
  | break_stmt 
  | continue_stmt 
  | goto_stmt 
  | return_stmt 

labeled_stmt
  : <IDENTIFIER> ":" stmt

if_stmt  
  : <IF> "(" expr ")" stmt [<ELSE> stmt] 

while_stmt 
  : <WHILE> "(" expr ")" stmt 

dowhile_stmt
  : <DO> stmt <WHILE> "(" expr ")" ";"

for_stmt 
  : <FOR> "(" [expr] ";" [expr] ";" [expr] ")" stmt 

switch_stmt
  : <SWITCH> "(" expr ")" "{" case_clauses "}"

case_clauses
  : (case_clause)* [default_clause]

case_clause
  : cases case_body
 
cases
  : <CASE> primary ":"

default_clause
  : <DEFAULT> ":" case_body

case_body
  : stmt

break_stmt 
  : <BREAK> ";" 

continue_stmt
  : <CONTINUE> ";"

goto_stmt
  : <GOTO> <IDENTIFIER> ";"

return_stmt 
  : <RETURN> ";" 
  | <RETURN> expr ";" 

expr 
  : term "=" expr 
  | term opassign_op expr 
  | expr10 

opassign_op 
  : "+=" 
  | "-=" 
  | "*=" 
  | "/=" 
  | "%=" 
  | "&=" 
  | "|=" 
  | "^=" 
  | "<<=" 
  | ">>=" 

Operator priority 
+-----------+--------------------+
| Priority  |      Operator      |
+-----------+--------------------+
|    10     |         ? :        |
+-----------+--------------------+
|     9     |         ||         |
+-----------+--------------------+
|     8     |         &&         |
+-----------+--------------------+
|     7     |  >, <, >=, ==, !=  |
+-----------+--------------------+
|     6     |     | (bit op)     |
+-----------+--------------------+
|     5     |     ^ (bit op)     |
+-----------+--------------------+
|     4     |     & (bit op)     |
+-----------+--------------------+
|     3     |      >>, <<        |
+-----------+--------------------+
|     2     |       +, -         |
+-----------+--------------------+
|     1     |     *, /, %        |
+-----------+--------------------+

expr10 
  : expr9 ["?" expr() ":" expr10] 

expr9 
  : expr8 ("||" expr8)* 

expr8 
  : expr7 ("&&" expr7)* 

expr7  
  : expr7 (   ">" expr6  
             | "<" expr6 
             | ">=" expr6 
             | "<=" expr6 
             | "==" expr6 
             | "!=" expr6 
             )* 

expr6 
  : expr5 ("|" expr5)* 

expr5 
  : expr4 ("^" expr4)* 

expr4 
  : expr3 ("&" expr3)* 

expr3 
  : expr2 ( ">>" expr2 | "<<" expr2)* 

expr2 
  : expr1 ( "+" expr1 | "-" expr1)* 

expr1 
  : term (  "*" term  
          | "/" term 
          | "%" term 
          )* 

term 
  : "(" type ")" term          // type casting 
  | unary 

unary 
  : "++" unary                 // pre ++ 
  | "--" unary                 // pre -- 
  | "+" term                   // unary +, positive 
  | "-" term                   // unary -, negative 
  | "!" term                   // Logical negation 
  | "*" term                   // Pointer dereference 
  | "&" term                   // address operator 
  | <SIZEOF> "(" type ")"      // sizeof(type) 
  | <SIZEOF> unary             // sizeof unary 
  | postfix                    // postfix  

postfix 
  : primary ("++"               // post ++ 
            |"--")              // post -- 
  | primary ("[" expr "]"       // array reference 
            |"." name           // class member reference 
            |"->" name          // class member pointer reference 
            |"(" args ")"       // function call 
            )* 

args 
  : [expr ("," expr)*] 

primary 
  : <INTEGER> 
  |<CHARACTER> 
  |<STRING> 
  |<IDENTIFIER> 
  |"(" expr ")" 



AST Node Tree =================================================================
        BaseNodeTy,
          NamedDeclTy,
            FunctionDeclTy,
            VariableDeclTy,
              ParamNodeTy,
            ConstantDeclTy,
          TypeNodeTy,
          ImportNodeTy,
          ArgsNodeTy, // function call arguments

          // Statements
          StmtNodeTy,
            BlockNodeTy,
            LabelNodeTy,
            ExprStmtNodeTy,
            IfNodeTy,
            WhileNodeTy,
            DoWhileNodeTy,
            ForNodeTy,
            CaseNodeTy,
            SwitchNodeTy,
            BreakNodeTy,
            ContinueNodeTy,
            GotoNodeTy,
            ReturnNodeTy,

          // Expressions
          ExprNodeTy,
            AbstractAssignNodeTy,
              AssignNodeTy,
              OpAssignNodeTy,
            AddressNodeTy, 
            BinaryOpNodeTy,
              LogicalAndNodeTy,
              LogicalOrNodeTy,
            CastNodeTy,
            CondExprNodeTy,
            FuncCallNodeTy,
            LValueNodeTy,
              ArrayRefNodeTy,
              DereferenceNodeTy,
              MemberRefNodeTy,
              PtrMemberRefNodeTy,
              VariableNodeTy,
            LiteralNodeTy,
              IntegerLiteralNodeTy,
              RealLiteralNodeTy, 
              StringLiteralNodeTy,
            SizeofExprNodeTy,
            SizeofTypeNodeTy,
            UnaryOpNodeTy,
              UnaryArithmeticOpNodeTy,
                PrefixOpNodeTy,
                SuffixOpNodeTy,

          // User defined types
          TypeDefinitionTy,
            CompositeTypeDefinitionTy,
              RecordDeclTy,
            TypedefNodeTy,
